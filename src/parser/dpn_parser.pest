// Basic characters
WHITESPACE = _{" "|"\t"|NEWLINE}
eq = _{"="}
int = {ASCII_DIGIT+}
float = {ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+}
lb = _{"("}
lc = _{"{"}
rb = _{")"}
rc = _{"}"}
separator = _{","}
ap = @{ASCII_ALPHANUMERIC+}
arrow = _{"->"}

// General elements
element = {(ASCII_ALPHA|float|ASCII_DIGIT)+}
tuple = {lb ~ (element ~ separator)* ~ element ~ rb}
element_set = _{lc ~ (element ~ separator)* ~ element ~ rc}

// Petri net elements
all_places = {element_set}
graph = {lc ~ (t_assign ~ separator)* ~ t_assign ~ rc}
t_assign = {(input_p ~ arrow)? ~ transition ~ (arrow ~ output_p)?}
input_p = {place_set}
transition = {name}
c_trans = {element_set}
output_p = {place_set}
place_set = _{lc ~ (place ~ separator)* ~ place ~ rc}
place = {place_name ~ tokens_wrapper?}
place_name = {name}
tokens_wrapper = _{"(" ~ tokens ~ ")"}
tokens = {ASCII_DIGIT+}
name = _{(ASCII_ALPHA|ASCII_DIGIT)+}

// Elements specifically for model checking
marking = {lb ~ (int ~ separator)* ~ int ~ rb}
markings = {lc ~ (marking ~ separator)* ~ marking ~ rc}
ap_assign = {lb ~ ap ~ separator ~ markings ~ rb}
AP_items = _{lc ~ (ap_assign ~ separator)* ~ ap_assign ~ rc}
AP = {"AP" ~ eq ~ AP_items}

// Petri net
petri_net = _{
	"P" ~ eq ~ #P = all_places ~
    "G" ~ eq ~ #G = graph ~
    "C" ~ eq ~ #C = c_trans ~
    "M" ~ eq ~ #M = tuple ~
    "L" ~ eq ~ #L = tuple
}

// Main
Main = _{SOI ~ petri_net ~ #AP_MAP = AP}

// Basic characters
WHITESPACE = _{" "|"\t"|NEWLINE}
eq = _{"="}
int = {ASCII_DIGIT+}
float = {ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+}
lb = _{"("}
lc = _{"{"}
rb = _{")"}
rc = _{"}"}
separator = _{","}
ap = @{ASCII_ALPHA+}
arrow = _{"->"}

// Petri net elements
element = {(ASCII_ALPHA|float|ASCII_DIGIT)+}
pair = {lb ~ element ~ separator ~  element ~ rb}
tuple = {lb ~ (element ~ separator)* ~ element ~ rb}
pair_set = {lc ~ (pair ~ separator)* ~ pair ~ rc}
element_set = _{lc ~ (element ~ separator)* ~ element ~ rc}

graph = {lc ~ (t_assign ~ separator)* ~ t_assign ~ rc}
t_assign = {(input_p ~ arrow)? ~ transition ~ (arrow ~ output_p)?}
input_p = {place_set}
transition = {name}
output_p = {place_set}
place_set = _{lc ~ (place ~ separator)* ~ place ~ rc}
place = {name}
name = _{(ASCII_ALPHA|ASCII_DIGIT)+}

// Elements specifically for model checking
marking = {lb ~ (int ~ separator)* ~ int ~ rb}
markings = {lc ~ (marking ~ separator)* ~ marking ~ rc}
ap_assign = {lb ~ ap ~ separator ~ markings ~ rb}
AP_items = _{lc ~ (ap_assign ~ separator)* ~ ap_assign ~ rc}
AP = {"AP" ~ eq ~ AP_items}

// Petri net
petri_net = _{
    "G" ~ eq ~ #G = graph ~
    "M" ~ eq ~ #M = tuple ~
    "L" ~ eq ~ #L = tuple
}

// Main
Main = _{SOI ~ petri_net ~ #AP_MAP = AP}
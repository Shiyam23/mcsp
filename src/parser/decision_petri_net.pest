// Basic characters
WHITESPACE = _{" "|"\t"|NEWLINE}
eq = _{"="}
int = {ASCII_DIGIT+}
float = {ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+}
lb = _{"("}
lc = _{"{"}
rb = _{")"}
rc = _{"}"}
separator = _{","}
ap = @{ASCII_ALPHA+}
comp = {"<=" | "<" | ">=" | ">"}

// Petri net elements
element = {(ASCII_ALPHA|float|ASCII_DIGIT)+}
pair = {lb ~ element ~ separator ~  element ~ rb}
tuple = {lb ~ (element ~ separator)* ~ element ~ rb}
pair_set = {lc ~ (pair ~ separator)* ~ pair ~ rc}
element_set = {lc ~ (element ~ separator)* ~ element ~ rc}

// pctl related elements
phi_id = {"phi"}
Phi_id = {"Phi"}
true = {"tt"}
and_op = _{"AND"}
not_op = _{"NOT "}
prob = {"P" ~ lb ~ phi ~ separator ~ comp ~ float ~ rb}

// Elements specifically for model checking
marking = {lb ~ (int ~ separator)* ~ int ~ rb}
markings = {lc ~ (marking ~ separator)* ~ marking ~ rc}
ap_assign = {lb ~ ap ~ separator ~ markings ~ rb}
AP_items = _{lc ~ (ap_assign ~ separator)* ~ ap_assign ~ rc}
AP = {"AP" ~ eq ~ AP_items}
reward_assign = {lb ~ element_set ~ separator ~ float ~ rb}
reward = {lc ~ reward_assign+ ~ rc}

// Expressions
Phi_and = {and_op ~ lb ~ Phi ~ separator ~ Phi ~ rb}
Phi_not = {not_op ~ Phi}
phi_next = {"NEXT " ~ Phi}
phi_until = {Phi ~ "UNTIL" ~ Phi}
Phi = _{true | prob | Phi_and | Phi_not | "(" ~ Phi ~ ")" | ap}
phi = _{ phi_next | phi_until | "(" ~ phi ~ ")"}

petri_net = _{
    "P" ~ eq ~ #P = element_set ~
	"T" ~ eq ~ #T = element_set ~
    "I" ~ eq ~ #I = pair_set ~
    "O" ~ eq ~ #O = pair_set ~
    "M" ~ eq ~ #M = tuple ~
    "L" ~ eq ~ #L = tuple ~
    "C" ~ eq ~ #C = element_set ~
    "R" ~ eq ~ #R = reward
}

// Main
Main = _{SOI ~ petri_net ~ #AP_MAP = AP ~ #PHI = Phi ~ EOI}